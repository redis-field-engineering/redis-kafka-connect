/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2021-2023 The Redis Kafka Connect authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
dependencies {
    implementation group: 'com.redis', name: 'lettucemod', version: lettucemodVersion
    implementation group: 'com.redis', name: 'spring-batch-redis', version: springBatchRedisVersion
    implementation group: 'org.apache.kafka', name: 'connect-api', version: kafkaVersion
    implementation group: 'org.apache.kafka', name: 'connect-json', version: kafkaVersion
    implementation group: 'org.apache.commons', name: 'commons-pool2', version: commonsPoolVersion
    implementation group: 'com.github.jcustenborder.kafka.connect', name: 'connect-utils', version: connectUtilsVersion
    implementation group: 'com.github.jcustenborder.kafka.connect', name: 'connect-utils-jackson', version: connectUtilsVersion
    
    testImplementation group: 'com.github.jcustenborder.kafka.connect', name: 'connect-utils-testing', version: connectUtilsVersion
    testImplementation group: 'org.awaitility', name: 'awaitility', version: awaitilityVersion
    testImplementation group: 'org.mockito', name: 'mockito-core', version: mockitoVersion
    testImplementation group: 'com.redis.testcontainers', name: 'testcontainers-redis-junit', version: testcontainersRedisVersion
}

eclipse {
    project {
        name = 'redis-kafka-connect-core'
    }
}

jar.finalizedBy shadowJar

import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation
import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "com.redis.kafka.connect.shaded"
}

tasks.shadowJar.dependsOn tasks.relocateShadowJar

compileJava {
    options.release = 8
}


def archiveFilename = "redis-redis-kafka-connect"
def releaseDate = DateTimeFormatter.ISO_LOCAL_DATE.format(LocalDateTime.now())

tasks.register('prepareConfluentArchive', Copy) {
    group = "Confluent"
    description = "Prepares the Confluent archive file for Confluent Hub"
    dependsOn tasks.shadowJar
    
    def baseDir = "$archiveFilename-${project.version}"
    from('config/archive/manifest.json') {
        expand(projectVersion: "${project.version}", releaseDate: "${releaseDate}")
    }
    from('config/archive/assets') {
        into 'assets'
    }
    from(layout.buildDirectory.dir('libs')) {
        include "${project.name}-${project.version}-all.jar"
        into 'lib'
    }
    from('../..') {
        include 'README.adoc'
        include 'LICENSE'
        into 'doc'
    }
    into layout.buildDirectory.dir("confluentArchive/${baseDir}")
}

tasks.register('createConfluentArchive', Zip) {
    group = "Confluent"
    description = "Creates the Confluent archive file to be released to Confluent Hub"
    dependsOn tasks.prepareConfluentArchive
    from(layout.buildDirectory.dir('confluentArchive'))
    archiveBaseName = ""
    archiveAppendix = archiveFilename
    destinationDirectory = layout.buildDirectory.dir('confluent')
    entryCompression = org.gradle.api.tasks.bundling.ZipEntryCompression.STORED
}

build.finalizedBy createConfluentArchive