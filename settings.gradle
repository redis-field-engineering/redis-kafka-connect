/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2021-2023 The Redis Kafka Connect authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath "gradle.plugin.com.github.johnrengelman:shadow:$shadowPluginVersion"
        classpath "org.ajoberstar:gradle-git-publish:$gitPluginVersion"
        classpath "org.kordamp.gradle:guide-gradle-plugin:$kordampPluginVersion"
        classpath "org.kordamp.gradle:java-project-gradle-plugin:$kordampPluginVersion"
        classpath "org.kordamp.gradle:jacoco-gradle-plugin:$kordampPluginVersion"
        classpath "org.kordamp.gradle:kordamp-parentbuild:$kordampBuildVersion"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$bootPluginVersion"
    }
}

apply plugin: 'org.kordamp.gradle.kordamp-parentbuild'

rootProject.name = 'redis-kafka-connect'

projects {
    directories = ['core', 'docs']

    plugins {
        all {
            id 'eclipse'
        }
        path(':') {
            id 'org.kordamp.gradle.java-project'
        }
        path(':guide') {
            id 'org.kordamp.gradle.guide'
            id 'org.ajoberstar.git-publish'
        }
        dirs(['core']) {
            id 'java-library'
            id 'org.springframework.boot'
            id 'io.spring.dependency-management'
        }
        path(':redis-kafka-connect') {
            id 'com.github.johnrengelman.shadow'
        }
    }
}

enforce {
    mergeStrategy = 'append'
    rule(enforcer.rules.EnforceBytecodeVersion) { r ->
        // search only on compile and runtime classpaths
        enabled = false
        r.configurations.addAll(['compileClasspath', 'runtimeClasspath'])
        r.maxJdkVersion = '1.8'
    }
    rule(enforcer.rules.BanDuplicateClasses) { r ->
        // search only on compile and runtime classpaths
        r.configurations.addAll(['compileClasspath', 'runtimeClasspath'])
    }
    rule(enforcer.rules.ForceDependencies) { r ->
        r.dependencies.addAll "com.redis:lettucemod:$lettucemodVersion",
            "org.testcontainers:testcontainers:$testcontainersVersion"
    }
}